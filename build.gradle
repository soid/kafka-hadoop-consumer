group 'com.baynote'
ext.ver = '0.8.0'

// To release, run:
// ./gradlew clean build uploadArchives -Drelease
// otherwise it'll build a snapshot
if (System.properties.getProperty("release") == null) {
    version ver + "-SNAPSHOT"
} else {
    version ver
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

jar {
    baseName = 'kafka-hadoop-consumer'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

assemble.dependsOn.remove(signArchives)
install.dependsOn.remove(signArchives)

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(signArchives)) {
        tasks.withType(Sign){
            enabled = false
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

// dependencies below

ext.kafka = "0.8.2.1"
ext.scala = "2.11"
ext.hadoop = "2.2.0"

dependencies {
    // kafka
    compile "org.apache.kafka:kafka_${scala}:${kafka}"

    // hadoop
    compile "org.apache.hadoop:hadoop-client:${hadoop}"
    compile "org.apache.hadoop:hadoop-common:${hadoop}"
    compile "org.apache.hadoop:hadoop-mapreduce-client-core:${hadoop}"

    // other dependencies
    compile "com.google.guava:guava:12.0.1"
    compile "commons-io:commons-io:2.1"
    compile "org.json:json:20140107"
    compile "org.slf4j:slf4j-api:1.6.1"
    compile "com.101tec:zkclient:0.6"

    // test libs
    testCompile 'org.mockito:mockito-core:1.9.0'
    testCompile 'info.batey.kafka:kafka-unit:0.3'
    testCompile group: 'junit', name: 'junit', version: '4.8.2'
}


// Maven Central Upload
// Copied from http://central.sonatype.org/pages/gradle.html

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Kafka Hadoop Consumer'
                packaging 'jar'
                description 'Reading Kafka input in Hadoop map jobs'
                url 'https://github.com/soid/kafka-hadoop-consumer'

                scm {
                    connection 'scm:git:https://github.com/soid/kafka-hadoop-consumer.git'
                    developerConnection 'scm:git:https://github.com/soid/kafka-hadoop-consumer.git'
                    url 'https://github.com/soid/kafka-hadoop-consumer.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'soid'
                        name 'Greg Temchenko'
                        email 'soid@dicefield.com'
                    }
                }
            }
        }
    }
}
